buildscript {
    repositories {
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        maven { url 'https://maven.aliyun.com/repository/spring-plugin' }
        maven { url 'https://maven.aliyun.com/repository/spring' }
    }

    dependencies {
        classpath "io.spring.javaformat:spring-javaformat-gradle-plugin:0.0.23"
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:5.1.2'
        classpath group: 'com.bmuschko', name: 'gradle-docker-plugin', version: '6.6.1'
    }
}
plugins {
    id 'org.springframework.boot' version '2.3.3.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.10.RELEASE' apply false
    id 'com.bmuschko.docker-spring-boot-application' version '6.6.1' apply false
}

ext {
    set('springBootVersion', "2.3.3.RELEASE")
    set('springCloudVersion', "Hoxton.SR7")
}

subprojects {

    apply plugin: 'io.spring.dependency-management'
    def module = file(".").listFiles().find { it.name == "build.gradle" }
    if (module != null) {
        apply plugin: 'java-library'
        apply from:  "${rootDir}/src/scripts/codestyle.gradle"
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        dependencies {
            compileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
            annotationProcessor 'org.projectlombok:lombok'
        }

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
        }

        test {
            useJUnitPlatform()
        }
    }

    group = 'com.cnegroup'
    version = '0.0.1-SNAPSHOT'

    repositories {
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://maven.aliyun.com/repository/spring' }
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}