buildscript {
    repositories {
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin'}
        maven { url 'https://maven.aliyun.com/repository/spring-plugin'}
        maven { url 'https://maven.aliyun.com/repository/public'}
        maven { url 'https://maven.aliyun.com/repository/spring'}
    }
}
plugins {
    id 'org.springframework.boot' version '2.3.3.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.10.RELEASE' apply false
}

ext {
    set('springBootVersion', "2.3.3.RELEASE")
    set('springCloudVersion', "Hoxton.SR7")
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    def module = file(".").listFiles().find {it.name == "build.gradle"}
    if (module != null) {
        apply plugin: 'java-library'
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        dependencies {
            compileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
            annotationProcessor 'org.projectlombok:lombok'
        }

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
        }

        test {
            useJUnitPlatform()
        }

    }

    group = 'com.cnegroup'
    version = '0.0.1-SNAPSHOT'

    repositories {
        maven { url 'https://maven.aliyun.com/repository/public'}
        maven { url 'https://maven.aliyun.com/repository/spring'}
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}
